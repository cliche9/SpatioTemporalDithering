#define DITHER_MODE_DISABLED 0
#define DITHER_MODE_PER_PIXEL_4X 1
#define DITHER_MODE_PER_PIXEL_16X 2
#define DITHER_MODE_PER_JITTER 3
#define DITHER_MODE_RUSSIAN_ROULETTE 4

#ifndef DITHER_MODE
#define DITHER_MODE DITHER_MODE_DISABLED
#endif

StructuredBuffer<uint> gStratifiedIndices;
StructuredBuffer<uint> gStratifiedLookUpTable;

float hash1D(float v)
{
    return frac(1.0e4 * sin(17.0 * v.x) * (0.1 + abs(sin(v.x))));
}

// Hash function from "Improved Alpha Testing Using Hashed Sampling"
float hash(float2 v)
{
    return frac(1.0e4 * sin(17.0 * v.x + 0.1 * v.y) * (0.1 + abs(sin(13.0 * v.y + v.x))));
}

// 3D hash function from "Improved Alpha Testing Using Hashed Sampling"
float hash3D(float3 v)
{
    return hash(float2(hash(v.xy), v.z));
}

// 4D hash function based on the 3D hash function
float hash4D(float4 v)
{
    return hash(float2(hash3D(v.xyz), v.w));
}

bool winRoulette(float alpha, uint frameIndex, float3 posW)
{
    float rng = hash4D(float4(posW, frameIndex));
    if(rng > alpha) return false;
    return true;
}

bool isCovered(uint2 pixel, float alpha, uint sampleIndex, uint sampleCount, uint frameIndex, float3 posW)
{
    float hashPixel = hash(float2(pixel));
    float rng = 0.5; // TODO should be a varying value
    //float rng = hash(float2(hashPixel, float(frameIndex/sampleCount)));
    //float rng = hash1D(float(frameIndex) / float(sampleCount));
    int R = int(floor(alpha * sampleCount + rng));
    if(R >= sampleCount) return true;
    //float rng2 = hash(float2(pixel));
    float rng2 = hash4D(float4(hashPixel, InstanceID(), GeometryIndex(), PrimitiveIndex() / 2u));
    //float rng2 = hash4D(float4(hash3D(posW), InstanceID(), GeometryIndex(), PrimitiveIndex() / 2u));
    //float rng2 = 0.5;
    int index = int(lerp(gStratifiedIndices[R], gStratifiedIndices[R + 1], rng2));
    uint mask = gStratifiedLookUpTable[index];
    
    if(mask & (1u << sampleIndex)) return true;
    return false;
}

float getPixelDitherThreshold(uint2 pixel)
{
    // use random offset to ensure that each primitive uses a different mask
    uint randomOffset = uint(hash3D(float3(InstanceID(), GeometryIndex(), PrimitiveIndex() / 2u))*7919);
    pixel.x += randomOffset % 4u;
    pixel.y += randomOffset / 4u;

    #if DITHER_MODE == DITHER_MODE_PER_PIXEL_4X
    pixel = pixel % 2u;
    float2x2 ditherMatrix = float2x2(
        float2(0.0, 2.0),
        float2(3.0, 1.0)
    );
    return (ditherMatrix[pixel.y][pixel.x] + 0.5) / 4.0;
    #else // assume 16 x
    pixel = pixel % 4u;
    float4x4 ditherMatrix = float4x4(
        float4(0.0, 8.0, 2.0, 10.0),
        float4(12.0, 4.0, 14.0, 6.0),
        float4(3.0, 11.0, 1.0, 9.0),
        float4(15.0, 7.0, 13.0, 5.0)
    );
    return (ditherMatrix[pixel.y][pixel.x] + 0.5) / 16.0;
    #endif
}